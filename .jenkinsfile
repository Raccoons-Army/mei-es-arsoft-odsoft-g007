pipeline {
    agent any

    triggers {
        githubPush()
    }

    tools {
        maven 'Maven 3.9.9'
        jdk 'JDK 17'
    }

    environment {
        PROJECT_DIR = "lms/"
        LOCAL_DEPLOY_DIR = "../deploy"
        DEPLOY_DIR = "deploy"
        DEPLOY_DIR_CFG = "cfg/"
        ENV_FILE_NAME = 'myCfg.env'
        APP_PORT = 2224
    }

    stages {

        stage('Build') {
            steps {
                dir(PROJECT_DIR) {
                    // Clean and package without running tests
                    sh 'mvn clean package -DskipTests'
                }
            }
        }

        stage('Static Code Analysis') {
            steps {
                dir(PROJECT_DIR) {
                    sh 'mvn pmd:pmd'
                    sh 'mvn spotbugs:spotbugs'
                }
            }
        }
       
        stage('Run Unit Tests') {
            steps {
                sh 'printenv'

                dir(PROJECT_DIR) {
                    // Run tests while ignoring failures
                    sh "mvn test -Dmaven.test.failure.ignore=true"
                }
            }
        }

         stage('Generate Coverage Report') {
            steps {
                dir(PROJECT_DIR) {
                    sh 'mvn jacoco:report'
                }
            }
        }

        stage('Mutation Testing') {
            steps {
                dir(PROJECT_DIR) {
                    sh 'mvn pitest:mutationCoverage'
                }
            }
        }

        stage('Run Integration Tests') {
            steps {
                dir(PROJECT_DIR) {
                    sh 'mvn failsafe:integration-test'
                }
            }
        }

        stage('Prepare to Deploy') {
            steps {
                script {

                    // clear the deploy directory
                    sh "rm -rf ${DEPLOY_DIR}/*"
                    
                    dir(PROJECT_DIR) {
                        // ensure the deployment directory exists
                        sh "mkdir -p ${LOCAL_DEPLOY_DIR}"

                        // copy the .jar file to the deploy directory (root of the workspace)
                        sh "cp target/*.jar ${LOCAL_DEPLOY_DIR}"
                    }

                }
            }
        }

        stage('Deploy to vsgate') {
            steps {
                script {
                    // Set DEPLOY_TARGET based on the branch name
                    def serverConfigName = ''
                    switch (env.BRANCH_NAME) {
                        case 'main':
                            serverConfigName = 'RAC_PROD'
                            break
                        case 'dev':
                            serverConfigName = 'RAC_DEV'
                            break
                        default:
                            serverConfigName = 'RAC_TEST'
                            break
                    }

                    sshPublisher(publishers: [
                        sshPublisherDesc(
                            configName: serverConfigName,
                            transfers: [
                                 // Clear the remote deploy directory
                                sshTransfer(
                                    sourceFiles: "",
                                    remoteDirectory: "",
                                    execCommand: "rm -rf ${DEPLOY_DIR}/* || true"
                                ),
                                // transfer the .jar file to the deploy directory > kill the existing process > start the new process
                                sshTransfer(
                                    sourceFiles: "${DEPLOY_DIR}/*.jar",
                                    remoteDirectory: "",
                                    execCommand: """
                                        # Kill the existing application process
                                        fuser -k ${APP_PORT}/tcp || true
                                        
                                        # load the environment variables
                                        export \$(grep -v '^#' ${DEPLOY_DIR_CFG}${ENV_FILE_NAME} | xargs)

                                        # source the environment variables
                                        source ${DEPLOY_DIR_CFG}${ENV_FILE_NAME}

                                        # boot the new application
                                        screen -dmS myJavaApp java -jar ${DEPLOY_DIR}/*.jar --server.port=${APP_PORT} --spring.config.location=${DEPLOY_DIR_CFG} > app.log 2>&1
                                    """
                                )
                            ],
                            usePromotionTimestamp: false,
                            useWorkspaceInPromotion: false,
                        )
                    ])
                }
            }
        }
    }
    
    post {
        always {
            archiveArtifacts artifacts: "${PROJECT_DIR}/target/failsafe-reports/*", allowEmptyArchive: true
            archiveArtifacts artifacts: "${PROJECT_DIR}/target/surefire-reports/*", allowEmptyArchive: true
            archiveArtifacts artifacts: "${PROJECT_DIR}/target/pit-reports/**/*", allowEmptyArchive: true
            archiveArtifacts artifacts: "${PROJECT_DIR}/target/site/jacoco/**/*", allowEmptyArchive: true
            archiveArtifacts artifacts: "${DEPLOY_DIR}/**/*", allowEmptyArchive: true
        }
    }
}
